version = "0.1.1-SNAPSHOT"

buildscript {
    ext {
        ghPages_version = version
        kotlin_version = '1.2.30'
        pom_explorer_version = '1.3.7'
        moshi_version = '1.5.0'
        kotlin_html_version = '0.6.8'
        kbox_version = '0.3.0'

        atrium_version = '0.6.0'
        mockito_kotlin_version = '1.5.0'
        dokka_version = '0.9.16'
        dokka_sourceMapping = 'tree/master'

        //spek.gradle
        jacoco_tool_version = '0.7.9'
        spek_version = '1.1.5'
        tutteli_spek_extensions = '0.2.0'

        //publish.gradle
        bintray_version = '1.7.3'
        publish_pkg_name = project.name
        github_url = "https://github.com/loewenfels/${project.name}"
        publish_vcsUrl = "${github_url}.git"

        //gh-pages.gradle
        docProjects = subprojects

        srcKotlin = 'src/main/kotlin'
        srcOwnProject = { String name -> "${project.projectDir}/${ext.srcKotlin}" }
        //jacoco-multi-project.gradle
        projectsWithoutSources = [] //currently all have sources

        apiProjects = subprojects.findAll { it.name.contains('api') }
        apiJvmProjects = ext.apiProjects.findAll { it.name.endsWith('-jvm') }
        apiJsProjects = ext.apiProjects.findAll { it.name.endsWith('-js') }
        commonApiProjects = ext.apiProjects - ext.apiJvmProjects - ext.apiJsProjects

        jvmProjects = subprojects - project("${rootProject.name}-gui") - apiProjects
        jacocoMulti = [
            sourceProjects: jvmProjects - project(':atrium') + apiJvmProjects,
            jacocoProjects: jvmProjects.findAll {
                it.name != "${rootProject.name}-maven-api" &&
                    it.name != "atrium"
            } - projectsWithoutSources + apiJvmProjects
        ]
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "ch.tutteli:tutteli-gradle-project-utils:${gradle.ext.tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-spek:${gradle.ext.tutteli_plugins_version}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'ch.tutteli.project.utils'

repositories {
    mavenCentral()
}

subprojects {
    it.version = rootProject.version

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/robstoll/copies" }
        maven { url "https://dl.bintray.com/robstoll/tutteli-jars" }
    }
}

configure(commonApiProjects) {
    apply plugin: 'kotlin-platform-common'
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    }
}

static def getApiProject(String name, String suffix) {
    return ":" + name.substring(0, name.indexOf(suffix))
}
configure(apiJsProjects) { subproject ->
    apply plugin: 'kotlin-platform-js'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
        expectedBy project(getApiProject(subproject.name, '-js'))
    }
}
configure(apiJvmProjects) { subproject ->
    apply plugin: 'kotlin-platform-jvm'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        expectedBy project(getApiProject(subproject.name, '-jvm'))
    }
}

configure(jvmProjects - apiProjects) {
    apply plugin: 'kotlin'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }
}

def publishedProjects = jvmProjects - project(':atrium')
configure(publishedProjects) {
    it.ext.publish_artifacts = []
}

def projectsWithSources = publishedProjects - projectsWithoutSources
configure(projectsWithSources) {
    apply plugin: 'org.jetbrains.dokka'

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/kdoc"
        linkMapping {
            dir = "$srcKotlin"
            url = "$github_url/${dokka_sourceMapping}/${project.name}/$srcKotlin"
            suffix = '#L'
        }
        linkMapping {
            dir = srcOwnProject('api')
            url = "$github_url/${dokka_sourceMapping}/${project.name}-api/$srcKotlin"
            suffix = '#L'
        }

        sourceDirs = files(srcKotlin)
    }

    task sourceJar(type: Jar) {
        from project.sourceSets.main.allSource
        classifier 'sources'
    }

    task javadocJar(type: Jar, dependsOn: dokka) {
        from dokka.outputDirectory
        classifier = 'javadoc'
    }

    it.ext.publish_artifacts += [sourceJar, javadocJar]
}

// has to be after the above block which define `it.ext.publish_artifacts`
configure(publishedProjects) { subproject ->
    subproject.ext {
        publish_artifactId = subproject.name
    }
    afterEvaluate {
        apply from: "${rootProject.projectDir}/gradle/scripts/publish.gradle", to: subproject

        def includeBuildTime = task('includeBuildTime') {
            doLast {
                project.tasks.withType(Jar) { jarTask ->
                    jarTask.manifest {
                        attributes('Build-Time': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ'))
                    }
                }
            }
        }

        task publishBintray {
            dependsOn includeBuildTime
            dependsOn bintrayUpload
            bintrayUpload.mustRunAfter(includeBuildTime)
        }
    }
}

configure(jacocoMulti.jacocoProjects) {
    apply plugin: 'ch.tutteli.spek'

    dependencies {
        testCompile project(':atrium')
        testCompile "ch.tutteli:tutteli-spek-extensions:$tutteli_spek_extensions"
    }
}


apply from: 'gradle/scripts/jacoco-multi-project.gradle'
apply from: 'gradle/scripts/gh-pages.gradle'

/*

Release & deploy a commit
--------------------------------
1. change version in build.gradle to X.Y.Z
2. update master:
    a) point to the tag
        1) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        2) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) commit (modified build.gradle, README.md and differences.md)
    c) git tag vX.Y.Z
    d) git push origin vX.Y.Z
3. deploy to bintray:
    a) gr publishBintray
    b) Log in to bintray, check and publish new jars
4. create release on github

Prepare next dev cycle
-----------------------
1. change version in build.gradle to X.Y.Z-SNAPSHOT
2. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
3. commit & push changes

*/
